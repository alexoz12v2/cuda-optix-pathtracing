cmake_minimum_required(VERSION 3.29) # C++ 20 modules require clang 16, cmake 3.28 at least

# ######################################################################################################################
# Policies
# ######################################################################################################################

# Enables the Visibility Property on all target types This means you can manage symbol visibility more easily through
# CMake properties without needing to manually handle compiler flags. Usage: This is helpful when building shared
# libraries, as it lets you define which symbols are exposed externally and which are hidden, improving binary size and
# load times.
cmake_policy(SET CMP0063 NEW)

# Enables the MSVC_RUNTIME_LIBRARY property on targets you can now use the MSVC_RUNTIME_LIBRARY property to specify
# whether you want to link against the static (/MT) or dynamic (/MD) version of the MSVC runtime instead of having to
# fiddle with compiler flags on Windows
cmake_policy(SET CMP0091 NEW)

# Enables POSITION_INDEPENDENT_CODE with old, you would need to pass -fPIC flag to compilers, on NEW, you can use the
# property POSITION_INDEPENDENT_CODE, depending on the target type (required for Shared Libraries and Executables)
cmake_policy(SET CMP0069 NEW)

# enable new MSVC debug information format specification mechanism if available this will embed debug information into
# the object file to allow ccache to cache it
if (POLICY CMP0141)
  cmake_policy(SET CMP0141 NEW)
endif ()

# ######################################################################################################################
# Project wide Properties and C++ Global Options
# ######################################################################################################################
set(CMAKE_EXPORT_COMPILE_COMMANDS ON)

# set the project version (used for versioning and CPack)
set(CMAKE_PROJECT_VERSION_MAJOR 1)
set(CMAKE_PROJECT_VERSION_MINOR 0)
set(CMAKE_PROJECT_VERSION_PATCH 0)
set(CMAKE_PROJECT_VERSION ${CMAKE_PROJECT_VERSION_MAJOR}.${CMAKE_PROJECT_VERSION_MINOR}.${CMAKE_PROJECT_VERSION_PATCH})

# Set required C++ Standard
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED TRUE)
set(CMAKE_C_STANDARD 11)
set(CMAKE_C_STANDARD_REQUIRED TRUE)
set(CMAKE_CXX_EXTENSIONS FALSE)
# minimum CUDA Compute Capability supported: Pascal -> 61
# In CMake’s CUDA_ARCHITECTURES, the -real and -virtual suffixes control what kind of GPU code is generated by NVCC.
#   real → generate native GPU machine code (SASS)
#   virtual → generate PTX (virtual ISA)
# What each one means
#   70-real
#      Compiles directly to SASS for sm_70 (Volta)
#          Fast startup (no JIT)
#          Best performance
#          Binary is larger
#          Will only run on GPUs compatible with sm_70
#       Equivalent NVCC flags:
#          -gencode arch=compute_70,code=sm_70
#   70-virtual
#       Generates PTX only, not final machine code
#           PTX is JIT-compiled at runtime by the NVIDIA driver
#           Slower startup (JIT cost)
#           Smaller binary
#           Can run on future GPUs (forward compatibility)
#       Equivalent NVCC flags:
#           -gencode arch=compute_72,code=compute_72
# Example:
##  set_property(TARGET tgt PROPERTY CUDA_ARCHITECTURES
##    70-real
##    72-virtual
##  )
##  This means:
##  Ship optimized native code for Volta GPUs (sm_70)
##  Also embed PTX fallback so:
##  Newer GPUs can still run the code
##  The driver can JIT for architectures you didn’t explicitly compile
set(CMAKE_CUDA_ARCHITECTURES "61-virtual")

macro(dmt_set_option var default type docstring)
  if (NOT DEFINED ${var})
    set(${var} ${default})
  endif ()
  set(${var}
    ${${var}}
    CACHE ${type} ${docstring} FORCE)
endmacro()

# Build Type -> Ignored on multi config generators, so removed
# dmt_set_option(CMAKE_BUILD_TYPE Debug STRING "Choose the type of build (eg Debug, Release)")

# docs generation
dmt_set_option(DMT_GENERATE_DOCS OFF BOOL "Generate Docs")

dmt_set_option(DMT_GENERATE_TRAINING OFF BOOL "Generate Training Targets")

# ######################################################################################################################
# Other Global CMake Configuration
# ######################################################################################################################

set(CMAKE_CUDA_FLAGS_INIT "" CACHE STRING "nothing" FORCE)
set(CMAKE_CUDA_HOST_FLAGS_INIT "" CACHE STRING "nothing" FORCE)
project(
  DumbTracer
  VERSION 1.0.0
  DESCRIPTION "CUDA Path Tracing"
)

enable_language(C)

set(CMAKE_MODULE_PATH "${PROJECT_SOURCE_DIR}/cmake")

# Link: https://forums.developer.nvidia.com/t/how-to-include-optix-8-0-in-cmake-project/269123
# set(CMAKE_CUDA_RUNTIME_LIBRARY Shared)

include(cmake/Config.cmake)
include(cmake/Dependencies.cmake)
include(cmake/SetupDoxygen.cmake)

dmt_define_environment()

include("${CMAKE_SOURCE_DIR}/cmake/CUDA.toolchain.cmake")
dmt_setup_cuda_toolkit_12_8()

# Enable CUDA After including toolkit
if (WIN32)
  message(STATUS "Printing Include environemnt variable")
  message(STATUS "$ENV{INCLUDE}")
endif ()
enable_language(CUDA)

# Dependencies after CUDA setup
dmt_setup_dependencies()

# grab python executable (msut be after `dmt_define_environment`). sets variable PYTHON_EXEC
dmt_find_python_executable()
block()
  execute_process(COMMAND ${PYTHON_EXEC} ${PROJECT_SOURCE_DIR}/scripts/test_python.py COMMAND_ECHO STDOUT
    OUTPUT_VARIABLE test_python_STDOUT)
  message(STATUS "${test_python_STDOUT}")
endblock()

# possible TODO: make this work
if (DMT_COMPILER_MSVC)
  dmt_set_option(DMT_GENERATE_PDB ON BOOL "True to generate PDB Symbols")
endif ()

dmt_set_option(DMT_ENABLE_SANITIZERS ON BOOL "Enable Sanitizers")
if (DMT_ENABLE_SANITIZERS AND NOT DMT_COMPILER_MSVC)
  string(APPEND CMAKE_CXX_FLAGS " -fno-omit-frame-pointer -fno-sanitize-recover=all -fsanitize=undefined")
endif ()

# output directory of eventual dlls and executables
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${PROJECT_BINARY_DIR}/bin) # TODO remove, put per target basis

dmt_set_option(CLANG_TIDY_EXECUTABLE clang-tidy STRING "clang-tidy executable program")
include(cmake/ClangTidy.cmake)

# custom target to copy assets from the `res/` directory (use `add_dependencies` in executable targets to have this copy
# happen automatically)
add_custom_target(
  dmt-copy-assets
  COMMAND ${CMAKE_COMMAND} -E copy_directory ${PROJECT_SOURCE_DIR}/res ${CMAKE_CURRENT_BINARY_DIR}/res
  COMMENT "copying in the build directory the assets directory")

add_custom_target(
  dmt-link-assets
  COMMAND ${CMAKE_COMMAND} -E create_symlink ${PROJECT_SOURCE_DIR}/res ${CMAKE_CURRENT_BINARY_DIR}/res
  COMMENT "Creating symbolic link for assets directory")

# Declare targets to Copy CUDA Device code inside the build/shaders directory
# add_custom_target(dmt-copy-cuda-device-code ALL COMMAND ${CMAKE_COMMAND} -E copy_directory ${OPTIX80_PATH}/include
# ${CUDAToolkit_INCLUDE_DIRS} ${PROJECT_BINARY_DIR}/shaders COMMAND ${CMAKE_COMMAND} -E copy_directory
# ${OPTIX80_PATH}/include/internal ${CUDAToolkit_INCLUDE_DIRS} ${PROJECT_BINARY_DIR}/shaders/internal COMMENT "Copy CUDA
# Device code source header files for JIT compilation" )

add_custom_target(
  dmt-copy-cuda-dlls ALL
  COMMAND ${PYTHON_EXEC} ${PROJECT_SOURCE_DIR}/scripts/copy_dlls.py ${CMAKE_BINARY_DIR}/bin
  COMMENT "Copy DLLs to bin")

set(dmt_cuda_toolkit_include_dir $ENV{CUDA_HOME}/include)

# add library code
add_subdirectory(src)

# possible todo: PCH

# build examples
add_subdirectory(examples)
add_subdirectory(cli)

dmt_set_option(DMT_ENABLE_TESTS OFF BOOL "ON To turn on tests, OFF to disable them")
if (DMT_ENABLE_TESTS)
  enable_testing()
  add_subdirectory(test)
endif ()

dmt_set_option(CLANG_FORMAT_EXECUTABLE clang-format STRING "clang-format executable program")
include(cmake/ClangFormat.cmake)

if (DMT_GENERATE_DOCS)
  dmt_setup_doxygen()
endif ()

foreach (lang CXX CUDA)
  set(CMAKE_${lang}_FLAGS "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_DEBUG "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_DEBUG_INIT "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_INIT "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_MINSIZEREL "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_MINSIZEREL_INIT "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_RELEASE "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_RELEASE_INIT "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_RELWITHDEBINFO "" CACHE STRING "nothing" FORCE)
  set(CMAKE_${lang}_FLAGS_RELWITHDEBINFO_INIT "" CACHE STRING "nothing" FORCE)
endforeach ()

if (DMT_GENERATE_TRAINING)
  add_subdirectory(training)
endif ()